ROUTINE %zml
	write "Entering multi line mode. Run with empty line. Stop with single ""."""
	new %zml
zml	xecute "zr  zi ""1 ;"" zs zml"_$j
	do {
		write !,$namespace_":zml:"_$i(%zml(1))_"> "
 		read %zml
		if %zml="." {
			xecute "zr  zs zml"_$j
			kill %zml 
			return
			} 
 		set %zml(2)="zl zml"_$j_" zi "" ""_%zml:+"_%zml(1)_" zs"
		xecute %zml(2)
	} while %zml]""
	write !
 	try { 
		do @("1^zml"_$j) 
		kill %zml}
 	catch %zml {
 		write "^",!,%zml.Name_$e($p(%zml.Location,"^"),2,*),!
		kill %zml  
		quit }
	goto zml
